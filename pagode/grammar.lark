start : func+

?expr : "se" op ", então" op ", mas se não for" expr -> cond
      | op

?op   : bit_or

?bit_or  : bit_or "|" bit_and
         | bit_and

?bit_and : bit_and "&" cmp
         | cmp

?cmp   : arith "menor que" arith  -> lt
       | arith "maior que" arith  -> gt
       | arith "como" arith  -> eq
       | arith

?arith : arith "junto com" term  -> add
       | arith "sem" term  -> sub
       | term

?term  : term "vezes" value  -> mul
       | term "dividido por" value  -> div
       | value

?value : NAME
       | INT
       | call
       | "(" expr ")"

call  : NAME "(" args ")"

args  : expr ("," expr)*

// double(x) = x + x
// add(x y) = x + y
func  : NAME "é" argn  "demais," expr
argn  : NAME ("," NAME)*

NAME  : /(?!\d)\w+/
INT   : /\d+/
OP    : /[-+*\/<>=&|]/
%ignore /\s+/
%ignore /Comentei[^\n]*/
